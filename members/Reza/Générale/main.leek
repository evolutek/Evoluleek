include("globals")
include("functions")
include("execute")
include("hunter")
include("prey")

/* Etape 1: Premier tour */
if (getTurn() == 1) {
	// Pas encore set: setWeapon(ARMES[0]["id"]) // Attention : coûte 1 PT
	debug("Je suis qui ? " + self)
	debug("Qui est mon ennemi ? " + enemy)
	debug("J'ai des TP ? " + getTP(self))
	debug("J'ai des MP ? " + getMP(self))
	say("YOOOOOOOOOOO " + getName(enemy) + "!!!")
}

/* Etape 2: Choisir l'état */
// 1) Soin au besoin
if (getTP(self) > getChipCost(CHIP_BANDAGE) and
	getLife() < getTotalLife() - 18 and
	canUseChip(CHIP_BANDAGE, self)) {
	debug("Soins ! TPs: " + getTP(self))
	useChip(CHIP_BANDAGE, self)
}

// 2) Préparation au combat au besoin
if (getCellDistance(getCell(), getCell(enemy)) < 15 and getLife()/getTotalLife() > 0.5) {
	if (getTP(self) > getChipCost(CHIP_MOTIVATION) and getCooldown(CHIP_MOTIVATION) == 0) {
		debug("Motivation ! TPs: " + getTP(self))
		useChip(CHIP_MOTIVATION, self)
	}
	if (getTP(self) > getChipCost(CHIP_STRETCHING) and getCooldown(CHIP_STRETCHING) == 0) {
		debug("Étirements ! TPs: " + getTP(self))
		useChip(CHIP_STRETCHING, self)
	}
}

// 3) Recharger les chip
var i = 0
while (i < count(ARMES))
{
	if (ARMES[i]["type"] == "puce" and
	   ARMES[i]["dispo"] < ARMES[i]["dispo_max"]) {
		ARMES[i]["dispo"]++
	}
	i++
}

// 4) Choix
if (getLife(enemy) < MAX_DEGATS) {
	execute()  // A termes calculer MAX_DEGATS
	say(arrayRandom(PHRASES[0])[0])
} else if (getLife() / getTotalLife() > (getLife(enemy) / getTotalLife(enemy)) - 0.15) {
	hunter()
	say(arrayRandom(PHRASES[1])[0])
} else {
	prey()
	say(arrayRandom(PHRASES[2])[0])
}
